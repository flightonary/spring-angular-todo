plugins {
    id 'java'
    id "com.github.node-gradle.node" version "3.0.1"
}

node {
    version = '16.0.0'
    download = true
    workDir = file("${rootProject.projectDir}/.gradle/nodejs")
}

//---------------------------
//    Installation
//---------------------------
task newAngularApp(type: NpxTask) {
    dependsOn nodeSetup
    command = '@angular/cli@11.2.11'
    args = ['new', "${rootProject.name}", '--directory=./', '--style=scss', '--routing=true']
}

task installBarista(type: NpxTask) {
    // https://barista.dynatrace.com/
    dependsOn newAngularApp
    command = 'ng'
    args = ['add', '@dynatrace/barista-components']
}

//---------------------------
//    Development
//---------------------------
task serveStandalone(type: NpxTask) {
  dependsOn npmInstall
  command = 'ng'
  args = ['serve', '--open']
}

//---------------------------
//    Build
//---------------------------
task buildForProduction(type: NpxTask) {
    dependsOn npmInstall
    command = 'ng'
    args = ['build', '-c', 'production']
    inputs.files('package.json', 'package-lock.json', 'angular.json', 'tsconfig.json', 'tsconfig.app.json')
    inputs.dir('src')
    inputs.dir(fileTree("node_modules").exclude(".cache"))
    outputs.dir('dist')
}


//---------------------------
//    Build jar
//---------------------------
jar.dependsOn 'buildForProduction'

jar {
    from "dist/${rootProject.name}" into 'static'
}
